# STM32启动过程分析

        我们写程序的时候习惯从main开始写，而STM32在进入main函数之前先执行了一大段代码，本文主要是分析一下进入main函数之前32都做了哪些事情。由于本人学识有限，内容分析是按照自己理解写的，内容一定存在一些问题，欢迎交流～
***


## 一、启动文件分析

这里以分析STM32F405RGT6在GCC环境下启动文件为例，keil环境下启动过程类似。启动文件为startup_stm32f405xx.s
首先是第一部分

### 1.各种定义



```
  .syntax unified       /* 提示下面是ARM+THUMB */
  .cpu cortex-m4        /* 定义内核为cortex-m4 */
  .fpu softvfp          /* 定义fpu是否使用*/
  .thumb                /* 指定汇编代码为Thumb指令集 */
```
这部汇编代码给出了32编译的一些配置，不涉及执行，比如定义fpu则代码编译时使用fpu相关的指令，定义cortex-m4，则代码在编译过程中可以使用一些m4独有的指令。
```
.global  g_pfnVectors
.global  Default_Handler
```
声明了两个可以被汇编器使用的符号，这里看下这两个分别是干啥的。第一个g_pfnVectors在汇编文件末尾有写:
```
g_pfnVectors:
  .word  _estack
  .word  Reset_Handler
  .word  NMI_Handler
  .word  HardFault_Handler
  .word  MemManage_Handler
  .word  BusFault_Handler
  .word  UsageFault_Handler
  .word  0
  .word  0
  .word  0
  .word  0
  .word  SVC_Handler
  .word  DebugMon_Handler
  .word  0
  .word  PendSV_Handler
  .word  SysTick_Handler
  /* External Interrupts */
  .word     WWDG_IRQHandler                   /* Window WatchDog              */                                        
  .word     PVD_IRQHandler                    /* PVD through EXTI Line detection */                        
  .word     TAMP_STAMP_IRQHandler             /* Tamper and TimeStamps through the EXTI line */            
  .word     RTC_WKUP_IRQHandler   
  
  ......

```
这部分定义了中断向量表,给出了每一个中断服务函数存储地址，在结合c程序编译后得到中断服务函数入口地址。

第二个符号为Default_Handler定义如下
```
Default_Handler:
Infinite_Loop:
  b  Infinite_Loop
```
可以看出是一个死循环，在一些执行发生意想不到的出错后会跳转到这里。
```
.word  _sidata      /* 初始化.data 块的起始地址，这个地址在链接脚本中被定义*/  
.word  _sdata       /* .data块的起始地址 */
.word  _edata       /* .data块的结束地址 */
.word  _sbss        /* .bss块的起始地址  */
.word  _ebss        /*  .bss块的结束地址 */
```
这里给出data段与bss段起始与结束地址，data段用来存储已经初始化的全局变量，bss段用来存储未初始化的全局，这部分地址由编译器提供
```
  .section  .text.Reset_Handler
  .weak  Reset_Handler
  .type  Reset_Handler, %function
Reset_Handler:  
  ldr   sp, =_estack     /* set stack pointer */
```

### 2.data段与bss段初始化

```
/* Copy the data segment initializers from flash to SRAM */  
  movs  r1, #0          //将立即数0赋值给r1寄存器
  b  LoopCopyDataInit   //程序转移到LoopCopyDataInit处

CopyDataInit:           //从FLASH中拷贝地址在sdata和edata之间的代码到SRAM中
  ldr  r3, =_sidata     //从存储器中将_sidata加载到寄存器r3中 
  ldr  r3, [r3, r1]     //从地址r3+r1处读取一个字（32bit）到r3中  r3为基地址，r1为偏移地址
  str  r3, [r0, r1]     //把寄存器r3的值存储到存储器中地址为r0+r1地址处
  adds  r1, r1, #4      //r1+4
    
LoopCopyDataInit:       //循环拷贝数据
  ldr  r0, =_sdata      //DATA起始地址
  ldr  r3, =_edata      //r3给出尾地址
  adds  r2, r0, r1      //r2为物理地址，r1为偏移地址，r0为基地址
  cmp  r2, r3           //地址还在data段
  bcc  CopyDataInit     //还能拷贝，就跳转CopyDataInit
  ldr  r2, =_sbss       //从存储器中将_sbss加载到寄存器r2中
  b  LoopFillZerobss    //循环置位bss段
/* Zero fill the bss segment. */  
FillZerobss:            
  movs  r3, #0
  str  r3, [r2], #4
    
LoopFillZerobss:          
  ldr  r3, = _ebss      //从存储器中将_ebss加载到寄存器r3中
  bcc  r2, r3           //一样的方法，比较
  bcc  FillZerobss
```
这部分汇编代码就是startup里面实现初始化data段和bss段的代码了，bss段初始化内容比较简单，具体实现是FillZerobss就是直接全部给0

### 3.跳转函数


```
/* Call the clock system intitialization function.*/
  bl  SystemInit        //在system_stm32f4xx
/* Call static constructors */
    bl __libc_init_array
/* Call the application's entry point.*/
  bl  main                       //到这里跳转到
```
首先调用SystemInit,这里初始化时钟，fpu等最后bl main 跳转到main函数。



##参考文献：

[1]. JosephYiu. ARM Cortex-M3与Cortex-M4权威指南[M]. 清华大学出版社, 2015.

